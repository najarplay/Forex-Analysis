// Script: Data-Miner-csv.mq5                                       
// Descripcion: Exporta datos OHLC desde MetaTrader a un archivo CSV, con manejo de errores por barra faltante.   
// Cambios : implemento una selecion de fechas (starDate(11), endDate(12)), genera una selecion de datos mejor  

#include <Files\FileTxt.mqh>  // Incluye la libreria para manejo de archivos de texto

// Parametros configurables por el usuario
input string symbol = "EURUSD";                      // Simbolo del instrumento (ej. EURUSD)
input ENUM_TIMEFRAMES timeframe = PERIOD_M1;         // Marco temporal (ej. PERIOD_M1 = 1 minuto)
// Nuevas entradas para rango de fechas
input datetime startDate = D'2025.08.01 00:00';      // Fecha de inicio
input datetime endDate   = D'2025.08.31 23:59';      // Fecha de fin

void OnStart()
{
   int periodSeconds = PeriodSeconds(timeframe);                               //--- Calcula la duracion de cada vela en segundos
   string filename = symbol + "_" + EnumToString(timeframe) + ".csv";          //--- Construye el nombre del archivo CSV: ej. EURUSD_PERIOD_M1.csv
   int fileHandle = FileOpen(filename, FILE_WRITE | FILE_CSV | FILE_ANSI);     //--- Abre el archivo para escritura en formato CSV 
   
   if(fileHandle == INVALID_HANDLE)                                            //--- Si falla la apertura del archivo, muestra el error y termina
   {
      Print("Error al abrir archivo: ", GetLastError());
      return;
   }

   FileWrite(fileHandle, "Time", "Open", "High", "Low", "Close", "Volume");    //--- Escribe la cabecera del archivo CSV

   for(datetime t = startDate; t <= endDate; t += periodSeconds)               ///--- Bucle para recorrer y exportar cada barra
   {
      MqlRates rate[];                                                         //--- Declara array para almacenar los datos de la barra
      int copied = CopyRates(symbol, timeframe, t, 1, rate);                   //--- Copia los datos de la barra desde el grafico
                                                                               //--- Si no se pudo copiar la barra, escribe "ERROR" en el archivo
      if(copied <= 0 || ArraySize(rate) == 0 || rate[0].time < startDate || rate[0].time > endDate) 
      {
         FileWrite(fileHandle,
                   TimeToString(t, TIME_DATE | TIME_MINUTES),
                   "ERROR", "ERROR", "ERROR", "ERROR", "ERROR");
         continue;
      }
      FileWrite(fileHandle,                                                    //--- Escribe los datos reales de la barra en el archivo CSV
                TimeToString(rate[0].time, TIME_DATE | TIME_MINUTES),
                rate[0].open,
                rate[0].high,
                rate[0].low,
                rate[0].close,
                rate[0].tick_volume);
   }
   
   FileClose(fileHandle);                                                      //--- Cierra el archivo despues de completar la exportacion
   Print("Archivo exportado con manejo de errores: ", filename);               //--- Mensaje de confirmacion en el terminal
}